plugins {
  id 'groovy'
}

repositories {

  mavenCentral()
  maven {
    url 'https://repo.jenkins-ci.org/public/'
  }
}

test {
  systemProperty 'groovy.grape.enable', 'false'

  reports {
    html.required = true
    html.outputLocation = file("$project.buildDir/reports/html")
  }
  ignoreFailures = true

  // show test results
  def results = []
  afterSuite { desc, result ->
    if (desc.className) { results << result }
  }

  // show summary
  doLast {
    println "Tests: ${results.sum { it.testCount }}" +
      ", Failures: ${results.sum { it.failedTestCount }}" +
      ", Errors: ${results.sum { it.exceptions.size() }}" +
      ", Skipped: ${results.sum { it.skippedTestCount }}"
  }
}

compileGroovy {
  groovyOptions.forkOptions.jvmArgs = [ '-Dgroovy.grape.enable=false' ]
  // @CNP annotation comes from the Jenkins Runtime instance
  exclude '**/pluginActive.groovy'
}

compileTestGroovy {
  groovyOptions.forkOptions.jvmArgs = [ '-Dgroovy.grape.enable=false' ]
}

dependencies {
  implementation group: 'com.cloudbees', name: 'groovy-cps', version: '3773.v505e0052522c'
  testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

  implementation group: 'org.jenkins-ci.main', name: 'jenkins-core', version: '2.460'
  implementation group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-step-api', version: '657.v03b_e8115821b_', ext: 'jar'
  implementation group: 'org.jenkinsci.plugins', name: 'pipeline-model-definition', version: '2.2221.vc657003fb_d93', ext: 'jar'
  implementation 'org.jenkins-ci.plugins:job-dsl-core:1.90'
  implementation group: 'org.jenkins-ci.plugins', name: 'scm-api', version: '616.ve67136f6c77d', ext: 'jar'

  testImplementation 'junit:junit:4.13.2'
  testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.4'
  testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.16.0'

  testImplementation (group: 'com.lesfurets', name: 'jenkins-pipeline-unit', version: '1.17')
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
  testImplementation group: 'net.javacrumbs.json-unit', name: 'json-unit-fluent', version: '2.36.0'

  testRuntimeOnly 'org.objenesis:objenesis:3.3'
}


sourceSets {
  jobs {
    groovy {
      srcDirs 'jobs'
      compileClasspath += main.compileClasspath
    }
  }

  main {
    groovy {
      srcDirs = ["$project.rootDir/src", "$project.rootDir/vars"]
    }
  }
  test {
    groovy {
      srcDirs = ["$project.rootDir/test"]
    }
    resources {
      srcDirs = ["$project.rootDir/testResources"]
    }
  }
}
