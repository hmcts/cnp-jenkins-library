plugins {
  id 'groovy'
}

repositories {

  mavenCentral()
  maven {
    url 'https://repo.jenkins-ci.org/releases/'
  }
}

test {
  systemProperty 'groovy.grape.enable', 'false'
  useJUnitPlatform()
  testLogging {
    showExceptions true
    exceptionFormat "short"
    showStandardStreams true
    events "failed"
    debug {
      events "failed", "passed", "skipped"
      exceptionFormat "full"
    }
    info {
      events "skipped", "failed"
      exceptionFormat "short"
    }
  }

  reports {
    html.required = true
    html.outputLocation = file("$project.buildDir/reports/html")
  }
  ignoreFailures = true

  // show test results
  def results = []
  afterTest { desc, result ->
    println "${desc.className.split("\\.")[-1]}: " +
      "${desc.name}: ${result.resultType}"
  }
  afterSuite { desc, result ->
    if (desc.className) { results << result }
  }

  // show summary
  doLast {
    println "Tests: ${results.sum { it.testCount }}" +
      ", Failures: ${results.sum { it.failedTestCount }}" +
      ", Errors: ${results.sum { it.exceptions.size() }}" +
      ", Skipped: ${results.sum { it.skippedTestCount }}"
  }
}

compileGroovy {
  groovyOptions.forkOptions.jvmArgs = [ '-Dgroovy.grape.enable=false' ]
  // @CNP annotation comes from the Jenkins Runtime instance
  exclude '**/pluginActive.groovy'
}

compileTestGroovy {
  groovyOptions.forkOptions.jvmArgs = [ '-Dgroovy.grape.enable=false' ]
}

dependencies {
  implementation ('org.codehaus.groovy:groovy-all:2.5.23') {
    exclude group: 'org.codehaus.groovy', module: 'groovy-ant'
  }
  implementation 'org.codehaus.groovy:groovy-dateutil:2.5.23'

  implementation group: 'com.cloudbees', name: 'groovy-cps', version: '3773.v505e0052522c'
  testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

  implementation (group: 'org.jenkins-ci.main', name: 'jenkins-core', version: '2.460') {
      exclude group: "org.codehaus.groovy", module: "groovy-all"
  }
  implementation group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-step-api', version: '657.v03b_e8115821b_', ext: 'jar'
  implementation group: 'org.jenkinsci.plugins', name: 'pipeline-model-definition', version: '2.2198.v41dd8ef6dd56', ext: 'jar'
  implementation 'org.jenkins-ci.plugins:job-dsl-core:1.87'
  implementation group: 'org.jenkins-ci.plugins', name: 'scm-api', version: '616.ve67136f6c77d', ext: 'jar'

  testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-2.5'
  testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.16.0'

  testImplementation 'org.junit.vintage:junit-vintage-engine:5.10.2'
  testImplementation (group: 'com.lesfurets', name: 'jenkins-pipeline-unit', version: '1.17') {
     exclude group: "org.codehaus.groovy", module: "groovy-all"
  }
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
  testImplementation group: 'net.javacrumbs.json-unit', name: 'json-unit-fluent', version: '2.36.0'

  testRuntimeOnly 'org.objenesis:objenesis:3.3'
}


sourceSets {
  jobs {
    groovy {
      srcDirs 'jobs'
      compileClasspath += main.compileClasspath
    }
  }

  main {
    groovy {
      srcDirs = ["src", "vars"]
    }
  }
  test {
    groovy {
      // todo: add test source directories withPipeline.*; withInfraPipeline.*; withNightlyPipeline.*
      srcDirs = ["test/uk/gov/hmcts/"]
    }
    resources {
      srcDirs = ["testResources"]
    }
  }
}
