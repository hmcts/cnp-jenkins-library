def mirrorHost = (System.getenv("NONPROD_SUBSCRIPTION_NAME") == "sandbox") ? 'sandbox-artifactory' : 'artifactory'
def mirrorUrl = "https://${mirrorHost}.platform.hmcts.net/artifactory/maven-remotes"

allprojects {
  repositories {
    maven {
      url mirrorUrl
    }
  }
  buildscript {
    repositories {
      maven {
        url mirrorUrl
      }
      mavenCentral()
    }
    dependencies {
      classpath  group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    }
  }

  task printVersionInit {
    doLast { println project.version }
  }

  task javaVersion {
    doLast { println project.sourceCompatibility }
  }
}

settingsEvaluated { settings ->
  settings.pluginManagement {
    repositories {
      maven {
        url mirrorUrl
      }
    }
  }
}

// Enable JSON dependency check reports for monitoring of CVEs and suppressions.
allprojects {
  afterEvaluate { project ->
    if (project.hasProperty('dependencyCheck')) {
      project.dependencyCheck.formats += 'json'
    }
  }
}

